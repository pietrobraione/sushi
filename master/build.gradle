plugins {
    id 'application'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

def glpkJavaVersion = '1.12.0'
def glpkJavaJniLocation = '/usr/local/lib/jni'
def javaHome = javaToolchains.compilerFor { languageVersion = javaVersion }.get().metadata.installationPath.asFile.toString()

repositories {
    maven {
        url = 'https://www.xypron.de/repository' //for glpk-java
    }
    flatDir {
    	dirs "${javaHome}/lib", "${project(':jbse').projectDir.toString()}/libs"
    }
}

dependencies {
    implementation 'org.gnu.glpk:glpk-java:' + glpkJavaVersion
    implementation name: 'tools'
    implementation project(':sushi-lib')
    implementation project(':jbse')
}

shadowJar {
    classifier = 'shaded'
    exclude 'tools.jar'
    exclude 'glpk-java*'
    relocate 'javassist', 'sushi.javassist'
    relocate 'jbse', 'sushi.jbse'
    relocate 'org.jacoco', 'sushi.org.jacoco'
    relocate 'org.objectweb', 'sushi.org.objectweb'
    relocate 'org.kohsuke', 'sushi.org.kohsuke'
}

build.dependsOn 'shadowJar'

eclipse {
    classpath { 
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
	        whenMerged { cp ->
	        	def entries = cp.getEntries()
	            def container = entries.find { it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }
	            container.nativeLibraryLocation = glpkJavaJniLocation
	        }
        }
    }
}

application {
    mainClassName = 'sushi.Main' //deprecated, but still used by the shadow plugin
    mainClass.set('sushi.Main')
}


jar {
    manifest {
        attributes 'Implementation-Title': 'SUSHI',
                   'Implementation-Vendor': 'University of Milano-Bicocca',
                   'Implementation-Version': archiveVersion
    }
}
